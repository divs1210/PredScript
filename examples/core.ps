interface spy;

interface isTrue;

interface map;
interface zip;
interface every;

interface gen_isListOf;
interface gen_isVector;
interface gen_isTuple;


function spy(x: isAny, tag: isString): isAny {
    println(tag + ": " + x)
    x
}


function isTrue(x: isAny): isBool {
    x == true
}


// stack overflow, we need loops / lazy seqs / stackless impl
function map(xs: isList, f: isFn): isList {
    if (isEmpty(xs))
        List()
    else
        xs
        .slice($this, 1)
        .map($this, f)
        .unshift($this, f(xs[0]))
}


// we don't have and / or yet ðŸ¤¦ðŸ½â€â™‚ï¸
// assuming both are of equal length
function zip(xs: isList, ys: isList): isList {
    if (isEmpty(xs))
        List()
    else
        xs
        .slice($this, 1)
        .zip($this, slice(ys, 1))
        .unshift($this, List(xs[0], ys[0]))
}


function every(xs: isList, f: isPred): isBool {
    if (isEmpty(xs))
        true
    else if (f(xs[0]))
        xs
        .slice($this, 1)
        .every($this, f)
    else
        false
}


// generic lists
memoized function gen_isListOf(t: isPred): isPred {
    interface isListOf;
    function isListOf(obj: isList): isBool {
        obj.every($this, AS(isPred, (x) => isA(t, type(x))))
    }
    derive(isList, isListOf)

    isListOf
}


// vectors are lists of fixed size
memoized function gen_isVector(elementType: isPred, vSize: isInt): isPred {
    let isParentType: isPred = gen_isListOf(elementType);

    interface isVector;
    function isVector(obj: isParentType): isBool {
        obj.size == vSize
    }
    derive(isParentType, isVector)

    isVector
}

let isListOfPreds: isPred = gen_isListOf(isPred);

// tuples are vectors of mixed types
memoized function gen_isTuple(preds: isListOfPreds): isPred {
    let isParentType: isPred = gen_isVector(isAny, size(preds));

    interface isTuple;
    function isTuple(obj: isParentType): isBool {
        obj
        .map($this, type)
        .zip($this, preds)
        .map($this, (pair) => {
            let isActual: isPred   = pair[0];
            let isExpected: isPred = pair[1];
            isA(isExpected, isActual);
        })
        .every($this, isTrue)
    }
    derive(isParentType, isTuple);

    isTuple
}


// complex numbers (test)
// ======================
let isComplexNumber: isPred =
    gen_isTuple(AS(isListOfPreds, List(isReal, isReal)));

function add(x: isComplexNumber, y: isComplexNumber): isComplexNumber {
    AS(
        isComplexNumber,
        List(
            x[0] + y[0],
            x[1] + y[1]
        )
    )
}

let c1: isComplexNumber = AS(isComplexNumber, List(1, 2));
let c2: isComplexNumber = AS(isComplexNumber, List(10, 20));

println(c1 + c2);

// let c3: isComplexNumber =
//    1
//    .List($this, 2)
//    .as(gen_isListOf(isAny), $this)
//    .as(gen_isVector(isAny, 2), $this)
//    .as(gen_isTuple(AS(isListOfPreds, List(isReal, isReal))), $this)
//    .as(isComplexNumber, $this);